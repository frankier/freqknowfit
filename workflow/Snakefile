from snakemake.utils import Paramspace
import pandas
from os.path import join as pjoin


def cnf(name, val):
    globals()[name] = config.setdefault(name, val)


# Directories
cnf("VOCABAQDATA_WORK", "vocabaqdata")
cnf("WORK", "work")


MODELS = {
    "glmLogit": "R",
    "glmProbit": "R",
    "glmCloglog": "R",
    # Doesn't make sense -- only useful for actual binomial not bernouli
    #"aodBetabinLogit": "R",
    #"aodBetabinCloglog": "R",
    # Cannot handle 
    # matrix bound exceeded -- row index too high in dmatrix::operator()
    # "glmmadmb": "R",
    "glmmTmbLogit": "R",
    "glmmTmbProbit": "R",
    "glmmTmbCloglog": "R",
    "tmbOi": "R",
    # Completely broken
    # Error while fitting vglm(...)
    # NA/NaN/Inf in 'y'
    #"vglm": "R",
    "statsmodelsGlmLogit": "py",
    "statsmodelsGlmProbit": "py",
    "statsmodelsGlmCloglog": "py",
}


DATASETS = {
    "blp": "blp.inventory.enriched.parquet",
    "dcp": "dcp.inventory.enriched.parquet",
    "dlp": "dlp.inventory.enriched.parquet",
    "dlp2": "dlp2.inventory.enriched.parquet",
    "ecp": "ecp.inventory.enriched.parquet",
    "elp": "elp.inventory.enriched.parquet",
    "flp": "flp.inventory.enriched.parquet",
    "spalex": "spalex.inventory.enriched.parquet",
    "svl12k": "svl12k.enriched.parquet",
}

SMALL_DATASETS = ["blp", "elp", "flp", "dlp", "dlp2"]
SMALL_DATASET_MODELS = ["glmmTmbLogit", "glmmTmbProbit", "glmmTmbCloglog"]

df = pandas.merge(
    pandas.DataFrame({"model": MODELS.keys()}),
    pandas.DataFrame({"dataset": DATASETS.keys()}),
    how="cross"
)
df.drop(df[df["model"].isin(SMALL_DATASET_MODELS) & ~df["dataset"].isin(SMALL_DATASETS)].index, inplace=True)
paramspace = Paramspace(df)


rule all:
    input:
        expand(pjoin(WORK, "{params}.parquets"), params=paramspace.instance_patterns)


rule fit_model:
    input:
        lambda wc: pjoin(VOCABAQDATA_WORK, DATASETS[wc.dataset])
    output:
        directory(pjoin(WORK, f"{paramspace.wildcard_pattern}.parquets"))
    params:
        r_script = srcdir("../freqknowfit/parametric/regress.R")
    run:
        from os import makedirs
        from os.path import dirname
        makedirs(dirname(output[0]), exist_ok=True)

        if MODELS[wildcards.model] == "R":
            prog = "Rscript " + params.r_script
        else:
            prog = "python -m freqknowfit.parametric.regress"

        shell(f"{prog} {wildcards.model} {input} {output}")
